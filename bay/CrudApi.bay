/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web.CRUD;

use Runtime.lib;
use Runtime.CoreStruct;
use Runtime.MessageRPC;
use Runtime.Monad;
use Runtime.RuntimeConstant;
use Runtime.RuntimeUtils;
use Runtime.Exceptions.ApiException;
use Runtime.Exceptions.RuntimeException;
use Runtime.ORM.Cursor;
use Runtime.ORM.ORMProviderInterface;
use Runtime.ORM.QueryBuilder;
use Runtime.Web.CRUD.CrudInterface;
use Runtime.Web.CRUD.CrudModel;
use Runtime.Web.CRUD.FieldInfo;


struct CrudApi extends CoreStruct implements CrudInterface
{
	
	string action = "";
	MessageRPC msg = null;
	QueryBuilder q = null;
	Dict item = null;
	int id = null;
	
	
	#ifdef BACKEND then
	
	
	/**
	 * Returns crud model name
	 */
	pure string crudModelName() => "";
	
	
	
	/**
	 * Returns crud table name
	 */
	pure string crudTableName() => "";
	
	
	
	/**
	 * Api init
	 */
	pure async fn<QueryBuilder, QueryBuilder> apiInit(CrudApi api) => api;
	
	
	
	/**
	 * Build fields
	 */
	pure memorize Dict<FieldInfo> buildFields()
	{
		string crud_model = static::crudModelName();
		fn buildFields = rtl::method(crud_model, "buildFields");
		return buildFields();
	}
	
	
	
	/**
	 * Add validate error
	 */
	pure fn<Dict, Dict> addValidateError(string field_name, string error) =>
		Dict<Collection<string>> (Dict<Collection<string>> response) use (field_name, error)
		{
			Collection<string> items = response[field_name];
			if (items == null) items = [];
			items = items.pushIm(error);
			return response.setIm(field_name, items);
		}
	;
	
	
	
	/**
	 * Validate field
	 */
	pure string validateField(CrudApi api, Dict item, FieldInfo field)
	{
		Collection<string> error = [];
		
		/* Check if required */
		var value = item -> CrudModel::getItemValue(field);
		if (field.required and (value === null or value === ""))
		{
			error = error.pushIm( _("Runtime.Web.CRUD", "This field must be filled") );
		}
		
		return error;
	}
	
	
	
	/**
	 * Validate item
	 */
	static fn<Dict, Dict> validateItem(CrudApi api) =>
		async Dict<string> (Dict item) use (api)
		{
			bool success = true;
			Dict<Collection<string>> response = {};
			
			/* Validate fields */
			Dict<FieldInfo> fields = static::buildFields();
			fields.each
			(
				void (FieldInfo field, string key) use (api, item, success, response)
				{
					Collection<string> error = static::validateField(api, item, field);
					if (error.count() > 0)
					{
						success = false;
						response = response.setIm(field.api_name, error);
					}
				}
			);
			
			if (not success)
			{
				return response.toDict();
			}
			
			return null;
		}
	;
	
	
	
	/**
	 * Validate
	 */
	static async fn<Dict, Dict> validate(CrudApi api) =>
		async Dict (Dict item) use (api)
		{
			Dict<string> validate = item -> await static::validateItem(api);
			if (validate)
			{
				throw new ApiException
				(
					_("Runtime.Web.CRUD", "Validation fields error"),
					RuntimeConstant::ERROR_VALIDATION,
					validate
				);
			}
			
			return item;
		}
	;
	
	
	
	/**
	 * Process item value
	 */
	pure fn<bool, Dict> processItemFlag(CrudApi api, FieldInfo field, Dict old_item = null) =>
		async bool (Dict item) use (api, field, old_item)
		{
			if (field.api_name == "id") return false;
			return true;
		}
	;
	
	
	
	/**
	 * Process item value
	 */
	pure fn<var, Dict> processItemValue(CrudApi api, FieldInfo field, Dict old_item = null) =>
		async var (Dict item) use (api, field, old_item)
		{
			
			/* Set default value */
			var value = item -> CrudModel::getItemValue(field);
			if (value === null or value === "")
			{
				value = field.default_value;
				return item -> CrudModel::setItemValue(field, value);
			}
			
			return item;
		}
	;
	
	
	
	/**
	 * Process item
	 */
	pure fn<var, Dict> processItem(CrudApi api, Dict old_item = null) =>
		async var (Dict item) use (api, old_item) => item;
	
	
	
	/**
	 * Process form item
	 */
	static Dict processFormItem(CrudApi api, Dict old_item = null) =>
		async Dict (Dict item) use (api, old_item)
		{
			Dict new_item = {};
			
			Dict<FieldInfo> fields = static::buildFields();
			Collection<string> keys = fields.keys();
			
			for (int i=0; i<keys.count(); i++)
			{
				FieldInfo field = fields[ keys[i] ];
				
				bool flag = item -> static::processItemFlag(api, field, old_item);
				if (flag)
				{
					new_item = item -> await static::processItemValue(api, field, old_item);
				}
			}
			
			new_item = new_item -> await static::processItem(api, old_item);
			
			return new_item;
		}
	;
	
	
	
	/**
	 * Convert item from database
	 */
	pure fn<Dict, Dict> fromDatabase(CrudApi api) =>
		Dict (Dict item) => item
	;
	
	
	
	/**
	 * Convert item to database
	 */
	pure fn<Dict, Dict> toDatabase(CrudApi api) =>
		Dict (Dict item) => item
	;
	
	
	
	/**
	 * Build search query
	 */
	pure QueryBuilder buildSearchQuery(CrudApi api)
	{
		ORMProviderInterface p = @ -> method getProvider(classof ORMProviderInterface);
		string table_name = static::crudTableName();
		string crud_model = static::crudModelName();
		
		fn default_order = rtl::method(crud_model, "getDefaultTableOrder");
		
		QueryBuilder q = new QueryBuilder()
			-> method select(table_name, p.prefix)
			-> method offset(0, 100)
			-> method order( default_order() )
		;
		
		return q;
	}
	
	
	
	/**
	 * Search
	 */
	static async MessageRPC search(MessageRPC msg)
	{
		ORMProviderInterface p = @ -> method getProvider(classof ORMProviderInterface);
		Collection<Dict> res = null;
		
		CrudApi api = static::newInstance
		{
			"action": "search",
			"msg": msg,
		};
		
		/* Build query */
		api <= q <= static::buildSearchQuery(api);
		
		/* Init api */
		api = await static::apiInit(api);
		
		/* Execute query */
		Cursor cursor = p -> await method execute(api.q);
		res = await cursor.fetchAll( static::fromDatabase(api) );
		cursor.close();
		
		Vector new_res = new Vector();
		fn processItem = static::processItem(api);
		for (int i=0; i<res.count(); i++)
		{
			var item = await processItem(res.item(i));
			new_res.push(item);
		}
		
		return msg -> method success{ "items": new_res };
	}
	
	
	
	/**
	 * Find items
	 */
	static async MessageRPC findItem(MessageRPC msg)
	{
		return msg;
	}
	
	
	
	/**
	 * Create query
	 */
	static fn<Dict, int> createQuery(CrudApi api) =>
		async int (Dict new_item)
		{
			string table_name = static::crudTableName();
			ORMProviderInterface p = @ -> method getProvider(classof ORMProviderInterface);
			
			/* Create query */
			int id = p
				-> await method insert
				(
					table_name,
					new_item
				)
				-> await method end
			;
			
			return id;
		}
	;
	
	
	
	/**
	 * Update query
	 */
	static fn<Dict, int> updateQuery(CrudApi api, int item_id) =>
		async void (Dict new_item) use (api, item_id)
		{
			string table_name = static::crudTableName();
			ORMProviderInterface p = @ -> method getProvider(classof ORMProviderInterface);
			
			/* Update query */
			p
				-> await method update
				(
					table_name,
					{
						"id": item_id,
					}
					new_item
				)
				-> await method end
			;
		}
	;
	
	
	
	/**
	 * Delete query
	 */
	static fn<Dict, void> deleteQuery(CrudApi api) =>
		async void (Dict row) use (api)
		{
			int item_id = row["id"];
			
			string table_name = static::crudTableName();
			ORMProviderInterface p = @ -> method getProvider(classof ORMProviderInterface);
			
			/* Delete query */
			p
				-> await method query
				(
					"delete from " ~ p.prefix ~ table_name ~ " where id=:id limit 1",
					{
						"id": item_id,
					}
				)
				-> await method end
			;
		}
	;
	
	
	
	/**
	 * Check item is null
	 */
	static fn<Dict, Dict> checkItemIsNull(CrudApi api) =>
		async Dict (Dict new_item)
		{
			if (new_item == null)
			{
				throw new ApiException
				(
					_("Runtime.Web.CRUD", "Converted to database item is null"),
					RuntimeConstant::ERROR_FATAL
				);
			}
		}
	;
	
	
	
	/**
	 * Get by id
	 */
	static fn<int, Dict> getById(CrudApi api) =>
		async Dict (int item_id) use (api)
		{
			string table_name = static::crudTableName();
			ORMProviderInterface p = @ -> method getProvider(classof ORMProviderInterface);
			
			/* Find item */
			Dict row = p
				-> await method query
				(
					"select * from " ~ p.prefix ~ table_name ~ " where id=:id limit 1",
					{
						"id": item_id,
					}
				)
				-> await method getOne
			;
			if (row == null)
			{
				throw new ApiException
				(
					_("Runtime.Web.CRUD", "Item not found"),
					RuntimeConstant::ERROR_ITEM_NOT_FOUND
				);
			}
			
			/* Convert old row */
			row = row -> static::fromDatabase(api);
			return row;
		}
	;
	
	
	
	/**
	 * Create
	 */
	static async MessageRPC create(MessageRPC msg)
	{
		Dict item = msg["data"]["item"];
		
		CrudApi api = static::newInstance
		{
			"action": "create",
			"msg": msg,
			"item": item,
		};
		
		/* Init api */
		api = await static::apiInit(api);
		
		/* Convert item */
		Dict new_item = item
			-> await static::validate(api)
			-> await static::processFormItem(api)
			-> static::toDatabase(api)
			-> static::checkItemIsNull(api)
		;
		
		/* Create query */
		int id = new_item -> await static::createQuery(api);
		item <= id <= id;
		
		return msg -> method success({ "id": id, "item": item }, _("Runtime.WEB.CRUD", "Success created"));
	}
	
	
	
	/**
	 * Update
	 */
	static async MessageRPC update(MessageRPC msg)
	{
		string table_name = static::crudTableName();
		ORMProviderInterface p = @ -> method getProvider(classof ORMProviderInterface);
		Dict item = msg["data"]["item"];
		
		CrudApi api = static::newInstance
		{
			"action": "update",
			"msg": msg,
			"item": item,
		};
		
		/* Get id */
		string item_id = item["id"];
		
		/* Init api */
		api = await static::apiInit(api);
		
		/* Convert item */
		Dict item = item
			-> await static::validate(api)
		;
		
		/* Find item */
		Dict row = item_id -> await static::getById(api);
		
		/* Covert item */
		Dict new_item = item
			-> await static::processFormItem(api, row)
			-> static::toDatabase(api)
			-> static::checkItemIsNull(api)
		;
		
		/* Update query */
		new_item -> await static::updateQuery(api, item_id);
		item <= id <= item_id;
		
		return msg -> method success({ "id": item_id, "item": item }, _("Runtime.WEB.CRUD", "Success updated"));
	}
	
	
	
	/**
	 * Delete
	 */
	static async MessageRPC delete(MessageRPC msg)
	{
		string table_name = static::crudTableName();
		ORMProviderInterface p = @ -> method getProvider(classof ORMProviderInterface);
		string item_id = msg["data"]["id"];
		
		CrudApi api = static::newInstance
		{
			"action": "delete",
			"msg": msg,
			"id": item_id,
		};
		
		/* Find item */
		Dict row = item_id -> await static::getById(api);
		
		/* Delete query */
		row -> await static::deleteQuery(api);
		
		return msg -> method success({ "id": item_id }, _("Runtime.WEB.CRUD", "Success deleted"));
	}
	
	#endif
	
	
}