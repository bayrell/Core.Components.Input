/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web.Crud;

use Runtime.BaseStruct;
use Runtime.Web.ApiResult;
use Runtime.Web.Crud.ValidationResult;


struct FormModel<T> extends BaseStruct
{
	T item = null;
	Dict item_pk = null;
	Dict dictionary = null;
    Dict params = null;
	int error_code = 0;
	string error_message = "";
	Dict field_result = null;
	bool is_loading = false;
	
	
	/**
	 * Returns primary key
	 */
	Dict getPrimaryKey()
	{
		return this.item_pk;
	}
	
	
	/**
	 * Set primary key
	 */
	FormModel setPrimaryKey(Dict item_pk)
	{
		return this.clone({ "item_pk": item_pk });
	}
	
	
	/**
	 * Set item
	 */
	FormModel setItem(T item)
	{
		return this.clone({ "item": item });
	}
	
	
	/**
	 * Set wait message
	 */
	FormModel setWaitMessage()
	{
		FormModel model = this;
		
		model <= error_code <= 0;
		model <= error_message <= "Wait please ...";
		
		return model;
	}
	
	
	/**
	 * Clear error message
	 */
	FormModel clearErrorMessage()
	{
		FormModel model = this;
		
		model <= error_code <= 0;
		model <= error_message <= "";
		
		return model;
	}
	
	
	/**
	 * Returns true if error
	 */
	bool isError() => this.error_code < 0;
	
	
	/**
	 * Returns true if success
	 */
	bool isSuccess() => this.error_code > 0;
	
	
	/**
	 * Set api result
	 */
	FormModel setApiResult(string event, ApiResult res)
	{
		if (res == null) return this;
		
		FormModel model = this;
		
		if (res.isSuccess())
		{
			Dict item = res["data", "item"];
			if (item)
			{
				model <= item <= item;
			}
			
			Dict pk = res["data", "pk"];
			model <= item_pk <= pk;
		}
		
		/* Set validation result */
		if (res.data != null and res.data.has("field_result"))
		{
			model <= field_result <= res.data["field_result"];
		}
		else
		{
			model <= field_result <= null;
		}
		
		model <= error_message <= res.getErrorMessage();
		model <= error_code <= res.getErrorCode();
		
		return model;
	}
	
	
	/**
	 * Returns form class
	 */
	string getErrorClass()
	{
		if (this.error_message == "") return "web_form__result--hide";
		if (this.error_code > 0) return "web_form__result--success";
		if (this.error_code < 0) return "web_form__result--error";
		return "";
	}
	
	
	/**
	 * Get field result
	 */
	Collection<string> getFieldResult(string field_name)
	{
		if (this.field_result == null) return [];
		if (not this.field_result.has(field_name)) return [];
		return this.field_result[field_name];
	}
}
