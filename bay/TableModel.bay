/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2021 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web.CRUD;

use Runtime.lib;
use Runtime.BaseStruct;
use Runtime.Web.App.PageModel;
use Runtime.Web.App.RemoteCallAnswer;
use Runtime.Web.App.RenderContainer;
use Runtime.Web.App.Request;


struct TableModel extends BaseStruct
{
	Collection<Dict> rows = null;
	int page = 0;
	int pages = 0;
	int delta = 5;
	int limit = 100;
	
	/* Crud error */
	int error_code = 1;
	string error_message = "";
	string error_name = "";
	
	
	
	/**
	 * Returns search params
	 */
	pure Dict getCrudSearchParams(Request request)
	{
		Map data = new Map();
		
		if (rtl::exists(request.query))
		{
			Vector search_filter = new Vector();
			Dict filter = request.query["filter"] |> default Dict null;
			if (filter)
			{
				Collection keys = filter.keys();
				for (int i=0; i<keys.count(); i++)
				{
					string key_name = keys[i];
					search_filter.pushValue([key_name, "=", filter[key_name]]);
				}
				data.setValue("filter", search_filter.toCollection());
			}
			
			string page = request.query.get("page", 1) - 1;
			if (page < 0) page = 0;
			data.setValue("page", page);
			data.setValue("limit", 20);
		}
		
		return data.toDict();
	}
	
	
	
	/**
	 * Search action
	 */
	static async RenderContainer searchAction(RenderContainer container, string object_name, Collection model_path)
	{
		/* Get page model */
		TableModel table_model = container.getPageModel(model_path);
		
		/* Get search params */
		Dict search_params = static::getCrudSearchParams(container.request);
		
		/* Remote call */
		RemoteCallAnswer answer = await container.busCall
		(
			"api://" ~ @.app_name ~ "/" ~ object_name ~ "/core.crud/search/",
			search_params
		);
		
		/* Set crud answer */
		table_model = static::setCrudAnswer(table_model, answer);
		
		/* Set page model */
		return container.setPageModelAttr(model_path, table_model);
	}
	
	
	
	/**
	 * Returns position of item
	 */
	pure int findPos(TableModel model, Dict find)
	{
		if (find == null) return -1;
		Collection<string> keys = find.keys();
		return model.rows.find
		(
			bool (Dict row) use (find, keys)
			{
				for (int i=0; i<keys.count(); i++)
				{
					string key = keys[i];
					var value1 = find[key];
					var value2 = row[key];
					if (value1 != value2) return false;
				}
				return true;
			}
		);
	}
	
	
	
	/**
	 * Find first item
	 */
	pure Dict getFirstItem(TableModel model, Dict find)
	{
		if (model.rows == null)
		{
			return null;
		}
		int pos = static::findPos(model, find);
		return model.rows.get(pos, null);
	}
	
	
	
	/**
	 * Add item
	 */
	pure TableModel addItem(TableModel model, Dict item)
	{
		if (model.rows == null)
		{
			model <= rows <= [];
		}
		model <= rows <= model.rows.pushIm(item);
		return model;
	}
	
	
	
	/**
	 * Prepend item
	 */
	pure TableModel prependItem(TableModel model, Dict item)
	{
		if (model.rows == null)
		{
			model <= rows <= [];
		}
		model <= rows <= model.rows.prependIm(item);
		return model;
	}
	
	
	
	/**
	 * Set item
	 */
	pure TableModel setItem(TableModel model, Dict find, Dict item)
	{
		if (model.rows == null)
		{
			model <= rows <= [];
		}
		
		int pos = static::findPos(model, find);
		if (pos != -1)
		{
			model <= rows <= model.rows.setIm(pos, item);
		}
		
		return model;
	}
	
	
	
	/**
	 * Remove item by id
	 */
	pure TableModel removeItem(TableModel model, Dict find)
	{
		if (model.rows == null)
		{
			return model;
		}
		
		int pos = static::findPos(model, find);
		if (pos != -1)
		{
			model <= rows <= model.rows.removeIm(pos);
		}
		
		return model;
	}
	
	
	
	/**
	 * Set crud answer
	 */
	pure TableModel setCrudAnswer(TableModel model, RemoteCallAnswer answer)
	{
		if (answer == null) return this;
		
		/* Answer */
		if (answer.isSuccess())
		{
			model <= rows <= answer.response["items"];
			model <= page <= answer.response["page"];
			model <= pages <= answer.response["pages"];
			model <= limit <= answer.response["limit"];
			model <= options <= answer.response["options"];
			model <= error_code <= rtl::ERROR_OK;
		}
		
		/* Exception */
		else
		{
			model <= error_code <= answer.code;
			model <= error_message <= answer.error_message;
			model <= error_name <= answer.error_name;
		}
		
		return model;
	}
}
