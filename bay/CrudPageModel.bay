/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web.Crud;

use Runtime.lib;
use Runtime.BaseStruct;
use Runtime.ModelProxy;
use Runtime.Exceptions.ApiException;
use Runtime.Exceptions.RuntimeException;
use Runtime.ORM.Connection;
use Runtime.ORM.Cursor;
use Runtime.ORM.Query;
use Runtime.ORM.Relation;
use Runtime.Web.ApiResult;
use Runtime.Web.BaseModel;
use Runtime.Web.Bus;
use Runtime.Web.RenderContainer;
use Runtime.Web.Crud.FormModel;
use Runtime.Web.Crud.StructBuilder;
use Runtime.Web.Crud.Rules.CrudRule;
use Runtime.Web.Crud.Rules.Filter;


struct CrudPageModel<T> extends BaseModel
{
	int page = 0;
    int pages = 0;
    Collection<T> items = null;
    Dict dictionary = {};
	Dict item_data = null;
	Dict search_data = null;
	FormModel item_form = null;
	FormModel save_form = null;
	FormModel delete_form = null;
	int error_code = 0;
	string error_message = "";
	string search = "";
	string action = "";
	
	
	/**
	 * Returns relation name
	 */
	pure string getRelationName() => "";
	
	
	/**
	 * Returns model name
	 */
	pure string getModelName() => "";
	
	
	/**
	 * Returns page type
	 */
	pure string getPageType() => "";
	
	
	/**
	 * Returns struct builder
	 */
	pure StructBuilder getStruct(fn build_fn = null)
	{
		string class_name = static::getModelName();
		string kind = static::getPageType();
		return StructBuilder::build(class_name, kind, build_fn);
	}
	
	
	/**
	 * Returns api name
	 */
	static string getApiName()
	{
		return "";
	}
	
	
	/**
	 * Returns primary key of model
	 */
	pure Dict pk(Dict item) => {};
	
	
	/**
	 * Returns page title
	 */
	string getPageTitle(string action) => "";
	
	
	/**
	 * Set CRUD action
	 */
	CrudPageModel setAction(string action = "") => this.clone({
		"action": action,
	});
	
	
	/**
	 * Set page
	 */
	CrudPageModel setPage(int page = 0) => this.clone({
		"page": page,
	});
	
	
	/**
	 * Route before
	 */
	async static CrudPageModel crudRouteBefore(ModelProxy model, RenderContainer container)
	{
		CrudPageModel page_model = model.data();
		ModelProxy model_item = static::getItemModelProxy(model);
		
		if (page_model.action == "add")
		{
			int id = (int)container.request.payload.get("id");
			model_item.setAttr("item_pk", null);
		}
		else if (
			page_model.action == "edit" or
			page_model.action == "delete" or
			page_model.action == "item"
		)
		{
			int id = (int)container.request.query.get("id");
			model_item.setAttr("item_pk", {
				"id": id,
			});
		}
	}
	
	
	/**
	 * Route after
	 */
	async static CrudPageModel crudRouteAfter(ModelProxy model, RenderContainer container)
	{
	}
	
	
	/**
	 * Event
	 */
	async static void event(ModelProxy model, string action, Dict data)
	{
		return data;
	}
	
	
	/**
	 * Set api result
	 */
	CrudPageModel setApiResult(string action, ApiResult res)
	{
		if (res == null) return this;
		
		CrudPageModel model = this;
		if (res.isSuccess())
		{
			if (action == "crudSearch")
			{
				model = model.copy({
					"items": res["data"]["items"],
					"pages": res["data"]["pages"],
					"page": res["data"]["page"],
				});
			}
			
			else if (action == "crudItem")
			{
				model <= item <= res["data", "item"];
			}
		}
		
		model <= error_message <= res.getErrorMessage();
		model <= error_code <= res.getErrorCode();
		
		return model;
	}
	
	
	/**
	 * Returns search data
	 */
	Dict getSearchData() => {
		"page": this.page,
	};
	
	
	/**
	 * Search items
	 */
	async static ApiResult crudSearch(ModelProxy model, Dict data = null)
	{
		model.setAttr("search_data", data);
		
		/* Extends search data */
		Dict post_data = model.data().getSearchData();
		if (data != null) post_data = data.concat(data);
		
		/* Process event */
		Dict data = await static::event(model, "crudSearchBefore", {
			"post_data": post_data,
		});
		
		/* Send api */
		ApiResult res = await static::remoteCall("actionSearch", post_data);
		
		/* Set api result */
		model.commit("setApiResult", "crudSearch", res);
		
		/* Process event */
		await static::event(model, "crudSearchAfter", {
			"post_data": post_data,
			"res": res
		});
		
		return res;
	}
	
	
	/**
	 * Returns item model
	 */
	Dict getItemModel()
	{
		if (this.action == "add") return this.save_form;
		else if (this.action == "edit") return this.save_form;
		else if (this.action == "delete") return this.delete_form;
		else if (this.action == "item") return this.item_form;
		return null;
	}
	
	
	/**
	 * Returns crud item data
	 */
	Dict getCrudItemData()
	{
		FormModel model = this.getItemModel();
		if (model == null) return null;
		
		return {
			"pk": model.item_pk
		};
	}
	
	
	/**
	 * Returns find item model
	 */
	static ModelProxy getItemModelProxy(ModelProxy model)
	{
		string action = model.data().action;
		if (action == "add" or action == "edit") return model.proxy("save_form");
		else if (action == "delete") return model.proxy("delete_form");
		else if (action == "item") return model.proxy("item_form");
		return null;
	}
	
	
	/**
	 * Add page
	 */
	async static void crudAdd(ModelProxy model)
	{
		await static::event(model, "crudAddBefore", {});
		
		/* Set api result */
		ModelProxy model_item = static::getItemModelProxy(model);
		if (model_item)
		{
			string class_name = static::getModelName();
			model_item.setAttr("item", rtl::newInstance(class_name));
		}
		
		await static::event(model, "crudAddAfter", {});
	}
	
	
	/**
	 * Find item
	 */
	async static ApiResult crudItem(ModelProxy model, Dict data = null)
	{
		model.setAttr("item_data", data);
		
		/* Get post data */
		CrudPageModel crud_model = model.data();
		Dict post_data = crud_model.getCrudItemData();
		if (data != null) post_data = data.concat(data);
		
		/* Process event */
		Dict data = await static::event(model, "crudItemBefore", {
			"post_data": post_data,
		});
		
		/* Send api */
		ApiResult res = await static::remoteCall("actionItem", post_data);
		
		/* Set api result */
		ModelProxy model_item = static::getItemModelProxy(model);
		if (model_item)
		{
			model_item.commit("setApiResult", "crudItem", res);
		}
		
		/* Process after */
		await static::event(model, "crudItemAfter", {
			"post_data": post_data, "res": res
		});
		
		return res;
	}
	
	
	/**
	 * Save form
	 */
	async static ApiResult crudSave(ModelProxy model)
	{
		/* Get post data */
		CrudPageModel crud_model = model.data();
		Dict post_data = crud_model.getCrudItemData();
		post_data <= item <= crud_model.save_form.item.toDict();
		
		/* Process before */
		Dict d = await static::event(model, "crudSaveBefore", {
			"post_data": post_data,
		});
		
		/* Send api */
		ApiResult res = await static::remoteCall("actionSave", post_data);
		
		/* Set api result */
		model.proxy(["save_form"]).commit("setApiResult", "crudSave", res);
		
		/* Set pk */
		if (res.isSuccess())
		{
			if (model.data().save_form.item_pk != null)
			{
				model.setAttr("action", "edit");
			}
			else
			{
				model.setAttr("action", "add");
			}
		}
		
		/* Process after */
		await static::event(model, "crudSaveAfter", {
			"post_data": post_data,
			"res": res,
		});
		
		return res;
	}
	
	
	/**
	 * Delete
	 */
	async static ApiResult crudDelete(ModelProxy model)
	{
		/* Get post data */
		CrudPageModel crud_model = model.data();
		Dict post_data = crud_model.getItemData();
		
		/* Process before */
		Dict d = await static::event(model, "crudDeleteBefore", {
			"post_data": post_data,
		});
		
		/* Send api */
		ApiResult res = await static::remoteCall("actionDelete", post_data);
		
		/* Set api result */
		model.proxy(["delete_form"]).commit("setApiResult", "crudDelete", res);
		
		/* Process after */
		await static::event(model, "crudDeleteAfter", {
			"post_data": post_data,
			"res": res,
		});
		
		return res;
	}
	
	
	
	#ifdef BACKEND then
	
	
	/**
	 * Returns database params
	 */
	pure Dict getDatabaseParams() => {};
	
	
	/**
	 * Returns database Connection
	 */
	pure Connection getDatabaseConnection()
	{
		Dict p = static::getDatabaseParams();
		string connection_name = p["name"];
		Connection conn = Connection::getConnection( connection_name );
		return conn;
	}
	
	
	/**
	 * Returns page limit
	 */
	static list getPageLimit(RenderContainer container)
	{
		int page = (int)(container.request.payload["data", "page"] |> default string 1);
		int limit = (int)(container.request.payload["data", "limit"] |> default string -1);
		
		if (page <= 1) page = 1;
		if (limit <= 0) limit = 10;
		if (limit > 100) limit = 100;
		
		return [page, limit];
	}
	
	
	/**
	 * Backend event
	 */
	async static void backendEvent(RenderContainer container, string action, Dict data)
	{
		/* Search */
		if (action == "actionSearchBefore" or
			action == "actionSearchOneBefore" or
			action == "actionItemBefore"
		)
		{
			Collection<CrudRule> rules = static::getRules(container);
			for (int i=0; i<rules.count(); i++)
			{
				CrudRule rule = rules[i];
				await rule.onSearchBefore(container);
			}
		}
		
		else if (action == "actionSearchAfter" or
			action == "actionSearchOneAfter" or
			action == "actionItemAfter"
		)
		{
			Collection<CrudRule> rules = static::getRules(container);
			for (int i=0; i<rules.count(); i++)
			{
				CrudRule rule = rules[i];
				await rule.onSearchAfter(container);
			}
		}
		
		/* Save */
		else if (action == "actionSaveBefore")
		{
			Collection<CrudRule> rules = static::getRules(container);
			for (int i=0; i<rules.count(); i++)
			{
				CrudRule rule = rules[i];
				await rule.onSaveBefore(container);
			}
		}
		
		else if (action == "actionSaveAfter")
		{
			Collection<CrudRule> rules = static::getRules(container);
			for (int i=0; i<rules.count(); i++)
			{
				CrudRule rule = rules[i];
				await rule.onSaveAfter(container);
			}
		}
		
		/* Delete */
		else if (action == "actionDeleteBefore")
		{
			Collection<CrudRule> rules = static::getRules(container);
			for (int i=0; i<rules.count(); i++)
			{
				CrudRule rule = rules[i];
				await rule.onDeleteBefore(container);
			}
		}
		
		else if (action == "actionDeleteAfter")
		{
			Collection<CrudRule> rules = static::getRules(container);
			for (int i=0; i<rules.count(); i++)
			{
				CrudRule rule = rules[i];
				await rule.onDeleteAfter(container);
			}
		}
		
		return data;
	}
	
	
	/**
	 * Returns rules
	 */
	pure Collection<CrudRule> getRules(RenderContainer container) =>
	[
	];
	
	
	/**
	 * Build filter
	 */
	static Query buildFilter(RenderContainer container, Query q)
	{
		Collection<string> filter_fields = static::getRules(container)
			|> .filter( lib::isInstance(classof Filter) )
		;
		
		Vector query_filter = new Vector();
		Collection request_filter = container.request.payload["data", "filter"];
		if (request_filter instanceof Collection)
		{
			for (int i=0; i<request_filter.count(); i++)
			{
				Collection filter = request_filter[i];
				if (filter instanceof Collection and filter.count() == 3)
				{
					string field_name = filter[0];
					string field_op = filter[1];
					string field_value = filter[2];
					if (
						rtl::isString(field_name) and
						rtl::isString(field_op) and
						(rtl::isString(field_value) or rtl::isInt(field_value))
					)
					{
						Filter rule = filter_fields.findItem(
							lib::equalAttr("api_name", field_name)
						);
						
						if (rule)
						{
							query_filter.pushValue([rule.field_name, field_op, field_value]);
						}
					}
				}
			}
		}
		
		return q.filter( query_filter.toCollection() );
	}
	
	
	/**
	 * Build search query
	 */
	static async Query buildSearchQuery(RenderContainer container, Query q) => q;
	
	
	/**
	 * Init api
	 */
	static async void initApi(RenderContainer container, Dict post_data, string action)
	{
		if (not container.backend_storage.has("api"))
		{
			container.backend_storage.setValue("api", new Map());
		}
		
		container.backend_storage["api"].setValue("action", action);
		container.backend_storage["api"].setValue("page_class_name", static::getClassName());
		container.backend_storage["api"].setValue("post_data", post_data);
	}
	
	
	/**
	 * Search items
	 */
	static async void searchItems(RenderContainer container)
	{
		string action = container.backend_storage["api", "action"];
		
		list res = static::getPageLimit(container);
		int page = res[0];
		int limit = res[1];
		int pages = 0;
		int rows = 0;
		
		if (action == "actionSearchOne")
		{
			limit = 1;
		}
		
		container.backend_storage["api"].setValue("page", page);
		container.backend_storage["api"].setValue("limit", limit);		
		
		/* Build query */
		string relation_name = static::getRelationName();
		Query q = rtl::method(relation_name, "query")()
			|> .select()
			|> .page(page - 1, limit)
		;
		q = static::buildFilter(container, q);
		q = static::buildSearchQuery(container, q);
		
		/* Execute query */
		Connection conn = static::getDatabaseConnection();
		Cursor c = await conn.execute(q, static::getDatabaseParams());
		Collection items = await c.fetchAll();
		if (q._limit > 0)
		{
			page = await c.getPage() + 1;
			pages = await c.getPages();
		}
		await c.close();
		
		/* Transform items */
		string model_name = static::getModelName();
		items = Relation::transformItems(items, model_name);
		
		container.backend_storage["api"].setValue("page", page);
		container.backend_storage["api"].setValue("pages", pages);
		container.backend_storage["api"].setValue("rows", rows);
		container.backend_storage["api"].setValue("items", items);
	}
	
	
	/**
	 * Action search
	 */
	static void actionSearch(RenderContainer container, Dict post_data)
	{
		/* Init api */
		await static::initApi(container, post_data, "actionSearch");
		if (container.response.error_name != "") return;
		
		/* Search before event */
		await static::backendEvent(container, "actionSearchBefore", {});
		if (container.response.error_name != "") return;
		
		/* Search items */
		await static::searchItems(container);
		
		/* Set answer */
		int page = container.backend_storage["api", "page"];
		int pages = container.backend_storage["api", "pages"];
		Collection<Dict> items = container.backend_storage["api", "items"];
		
		container.success({
			"data":
			{
				"page": page,
				"pages": pages,
				"items": items,
				"dictionary": [],
			}
		});
		
		/* Search after event */
		await static::backendEvent(container, "actionSearchAfter", {});
	}
	
	
	/**
	 * Action search
	 */
	static async void actionSearchOne(RenderContainer container, Dict post_data)
	{
		/* Init api */
		await static::initApi(container, post_data, "actionSearchOne");
		if (container.response.error_name != "") return;
		
		/* Search before event */
		await static::backendEvent(container, "actionSearchOneBefore", {});
		if (container.response.error_name != "") return;
		
		/* Search items */
		await static::searchItems(container);
		
		/* Set answer */
		int page = container.backend_storage["api", "page"];
		int pages = container.backend_storage["api", "pages"];
		Collection<Dict> items = container.backend_storage["api", "items"];
		
		container.success({
			"data":
			{
				"page": page,
				"pages": pages,
				"item": items[0],
				"dictionary": [],
			}
		});
		
		/* Search after event */
		await static::backendEvent(container, "actionSearchOneAfter", {});
	}
	
	
	/**
	 * Find item
	 */
	static async void findItem(RenderContainer container, Dict pk)
	{
		/* Query to database */
		string relation_name = static::getRelationName();
		
		/* Get primary key */
		fn getPrimaryFilter = rtl::method(relation_name, "getPrimaryFilter");
		Collection filter = getPrimaryFilter(pk, true);
		
		/* Build query */
		Query q = rtl::method(relation_name, "query")()
			|> .select()
			|> .filter(filter)
		;
		q = static::buildSearchQuery(container, q);
		
		/* Execute query */
		Connection conn = static::getDatabaseConnection();
		Cursor c = await conn.execute(q, static::getDatabaseParams());
		Relation item = await c.fetch();
		await c.close();
		
		if (!item)
		{
			return container.fail( new ApiException("Item not found") );
		}
		
		/* Set item */
		container.backend_storage["api"].setValue("item", item);
	}
	
	
	/**
	 * Create item
	 */
	static async void createItem(RenderContainer container)
	{
		/* Create relation */
		string relation_name = static::getRelationName();
		Relation item = rtl::newInstance(relation_name);
		
		/* Set item */
		container.backend_storage["api"].setValue("item", item);
	}
	
	
	/**
	 * Validate item
	 */
	static async void validateItem(RenderContainer container, StructBuilder builder, Dict new_item)
	{
		container <= response <= builder.validateApi(container.response, new_item);
	}
	
	
	/**
	 * Update item
	 */
	static async void updateItem(RenderContainer container, Relation item, Dict update_data)
	{
		Connection conn = static::getDatabaseConnection();
		Collection keys = update_data.keys();
		if (keys.count() > 0)
		{
			for (int i=0; i<keys.count(); i++)
			{
				string field_name = keys[i]
				item.set(field_name, update_data[field_name]);
			}
			
			await item.save(conn);
		}
	}
	
	
	/**
	 * Action item
	 */
	static async void actionItem(RenderContainer container, Dict post_data)
	{
		/* Init api */
		await static::initApi(container, post_data, "actionItem");
		if (container.response.error_name != "") return;
		
		/* Before event */
		await static::backendEvent(container, "actionItemBefore", {});
		if (container.response.error_name != "") return;
		
		/* Find item */
		Dict pk = container.request.payload["data", "pk"];
		await static::findItem(container, pk);
		if (container.response.error_name != "") return;
		
		/* Transform items */
		Relation item = container.backend_storage["api", "item"];
		string model_name = static::getModelName();
		BaseStruct item = item.toModel( model_name );
		
		/* Set answer */
		container.success({
			"data":
			{
				"item": item,
				"dictionary": [],
			}
		});
		
		/* After event */
		await static::backendEvent(container, "actionItemAfter", {});
	}
	
	
	/**
	 * Action save
	 */
	static async void actionSave(RenderContainer container, Dict post_data)
	{
		/* Init api */
		await static::initApi(container, post_data, "actionSave");
		if (container.response.error_name != "") return;
		
		/* Before event */
		await static::backendEvent(container, "actionSaveBefore", {});
		if (container.response.error_name != "") return;
		
		/* Find item */
		Dict pk = container.request.payload["data", "pk"];
		if (pk != null)
		{
			await static::findItem(container, pk);
			if (container.response.error_name != "") return;
		}
		else
		{
			await static::createItem(container);
		}
		
		StructBuilder builder = static::getStruct();
		if (builder == null)
		{
			throw new RuntimeException("Builder is null");
		}
		
		/* Purify item */
		Dict new_item = container.request.payload["data", "item"];
		new_item = builder.purify( new_item );
		
		/* Validate new item */
		await static::validateItem(container, builder, new_item);
		if (container.response.error_name != "") return;
		
		/* Update item */
		Relation item = container.backend_storage["api", "item"];
		Dict update_item = builder.data(new_item);
		await static::updateItem( container, item, update_item );
		if (container.response.error_name != "") return;
		
		/* Get new pk */
		Dict pk = item.getPrimaryKey();
		
		/* Find item */
		await static::findItem(container, pk);
		
		/* Transform items */
		Relation item = container.backend_storage["api", "item"];
		string model_name = static::getModelName();
		BaseStruct item = item.toModel( model_name );
		
		/* Set api result */
		container.success({
			"data":
			{
				"pk": pk,
				"item": item,
				"dictionary": [],
			}
		});
		
		/* After event */
		await static::backendEvent(container, "actionSaveAfter", {});
	}
	
	
	/**
	 * Delete item
	 */
	static async void deleteItem(RenderContainer container, Relation item)
	{
		Connection conn = static::getDatabaseConnection();
		item.delete(conn);
	}
	
	
	/**
	 * Action delete
	 */
	static async void actionDelete(RenderContainer container, Dict post_data)
	{
		/* Init api */
		await static::initApi(container, post_data, "actionDelete");
		if (container.response.error_name != "") return;
		
		/* Before event */
		await static::backendEvent(container, "actionDeleteBefore", {});
		if (container.response.error_name != "") return;
		
		/* Find item */
		Dict pk = container.request.payload["data", "pk"];
		await static::findItem(container, pk);
		if (container.response.error_name != "") return;
		
		/* Delete item */
		Relation item = container.backend_storage["api", "item"];
		await static::deleteItem(container, item);
		if (container.response.error_name != "") return;
		
		/* Set api result */
		container.success({
			"data":
			{
				"item": item,
				"dictionary": [],
			}
		});
		
		/* Before event */
		await static::backendEvent(container, "actionDeleteAfter", {});
	}
	
	
	#endif
	
}
