/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web.Crud;

use Runtime.BaseStruct;
use Runtime.Web.ApiResult;
use Runtime.Web.Bus;
use Runtime.Web.ModelProxy;
use Runtime.Web.RenderContainer;


struct CrudModel<T> extends BaseStruct
{
	int page = 0;
    int pages = 0;
	T item = null;
    Collection<T> items = null;
    Dict dictionary = null;
	Dict search_data = {};
	Dict find_item_data = {};
	int error_code = 0;
	string error_message = "";
	string search = "";
	
	
	/**
	 * Returns api name
	 */
	static string getApiName()
	{
		return "";
	}
	
	
	/**
	 * Get api url
	 */
	static string getApiUrl(string action, Dict data)
	{
		string api_name = static::getApiName();
		if (action == "crud_search") return api_name ~ ".crud_search";
		if (action == "crud_item") return api_name ~ ".crud_item";
		return "";
	}
	
	
	/**
	 * Before action
	 */
	async static void before(ModelProxy model, string action, Dict data)
	{
	}
	
	
	/**
	 * After action
	 */
	async static void after(ModelProxy model, string action, Dict data)
	{
	}
	
	
	/**
	 * Process api data
	 */
	static Dict processApiData(string action, Dict search_data)
	{
		return search_data;
	}
	
	
	/**
	 * Set api result
	 */
	CrudModel setApiResult(string action, ApiResult res)
	{
		CrudModel model = this;
		if (res.isSuccess())
		{
			if (action == "searchItems")
			{
				model = model.copy({
					"items": res["data"]["items"],
					"pages": res["data"]["pages"],
					"page": res["data"]["page"],
				});
			}
			
			else if (action == "findItem")
			{
				model <= item <= res["data", "item"];
			}
		}
		
		/* Search */
		if (action == "searchItems" and model.search_data.has("search"))
		{
			model <= search <= model.search_data["search"];
		}
		
		model <= error_message <= res.getErrorMessage();
		model <= error_code <= res.getErrorCode();
		
		return model;
	}
	
	
	/**
	 * Search items
	 */
	async static void searchItems(ModelProxy model)
	{
		/* Process before */
		await static::before(model, "searchItems", {});
		
		/* Send api */
		ApiResult res = await static::searchApi( model.data().search_data );
		
		/* Set api result */
		model.commit("setApiResult", "searchItems", res);
		
		/* Process after */
		await static::after(model, "searchItems", { "res": res });
	}
	
	
	/**
	 * Search api
	 */
	static async ApiResult searchApi(Dict search_data)
	{
		string url = static::getApiUrl("crud_search", {"search_data": search_data});
		
		search_data = static::processApiData("searchApi", search_data);
		ApiResult res = await Bus::callApi(url, search_data);
		
		return res;
	}
	
	
	/**
	 * Find item
	 */
	async static void findItem(ModelProxy model)
	{
		/* Process before */
		await static::before(model, "before", "findItem", {});
		
		/* Send api */
		ApiResult res = await static::findItemApi( model.data().find_item_data );
		
		/* Set api result */
		model.commit("setApiResult", "findItem", res);
		
		/* Process after */
		await static::after(model, "after", "findItem", { "res": res });
	}
	
	
	/**
	 * Get by id Api
	 */
	static async ApiResult findItemApi(Dict search_data)
	{
		search_data = static::processApiData("findItemApi", search_data);
		string url = static::getApiUrl("crud_item", {"search_data": search_data});
		
		ApiResult res = await Bus::callApi(url, search_data);
		
		return res;
	}
	
}
