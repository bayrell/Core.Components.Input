/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2018-2019 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web.CRUD;

use Runtime.Exceptions.RuntimeException;
use Runtime.Interfaces.IntrospectionClass;
use Runtime.CoreStruct;
use Runtime.lib;
use Runtime.MessageRPC;
use Runtime.RuntimeUtils;
use Runtime.ORM.Annotations.Orm;
use Runtime.ORM.Annotations.AutoIncrement;
use Runtime.ORM.Annotations.PrimaryKey;
use Runtime.ORM.Annotations.FieldType;
use Runtime.ORM.DataProviderInterface;
use Runtime.ORM.OrmQuery;
use Runtime.ORM.OrmResult;
use Runtime.Web.CRUD.ItemModel;
use Runtime.Web.CRUD.Annotations.CrudController;
use Runtime.Web.CRUD.Annotations.FormValue;
use Runtime.Web.CRUD.Annotations.TableValue;
use Runtime.Web.UI.LayoutModel;
use Runtime.Web.UI.RenderContainer;


struct CrudController extends CoreStruct
{
	/* Base */
	string action = "";
	string api_name = "";
	string model_name = "";
	
	/* Search */
	Dict<Collection> fields_info = null;
	Collection<string> fields = null;
	/*Collection<CrudFilterItem> filter = null;*/
	Collection<ItemModel> items = null;
	int page = 0;
	int start = 0;
	int total = 0;
	int limit = 0;
	int max_limit = 0;
	
	/* Current item */
	int index = 0;
	Dict pk = null;
	ItemModel item = null;
	
	/* Error */
	int error_code = 0;
	string error_str = "";
	
	
	/* Message */
	string orm_connection = "default";
	MessageRPC msg = null;
	
	
	
	/**
	 * Returns api name
	 */
	lambda string crudApiName() => "";
	
	
	
	#ifdef BACKEND then
	
	/**
	 * Returns table name
	 */
	lambda string crudTableName() => "";
	
	#endif
	
	
	
	/**
	 * Returns required modules
	 * @return Map<string>
	 */
	lambda Map<string> modules(CrudController crud) => 
	[
	];
	
	
	
	/**
	 * Returns required components
	 * @return Map<string>
	 */
	lambda Map<string> components(CrudController crud) => 
	[
	];
	
	
	
	/**
	 * Returns table fields
	 */
	lambda Collection<string> getTableFields(CrudController crud) => 
	[
	];
	
	
	
	/**
	 * Returns table field value
	 */
	lambda string getTableOrder(CrudController crud) => "";
	
	
	
	/**
	 * Returns form fields
	 */
	lambda Collection<string> getFormFields(CrudController crud) => 
	[
	];
	
	
	
	/**
	 * Returns label name
	 */
	lambda string getLabelName(string field_name, CrudController crud)
	{
		return "";
	}
	
	
	
	/**
	 * Returns table value
	 */
	lambda html getHtmlValue(LayoutModel layout, string field_name, var control, CrudController crud) => 
		crud.item.takeValue(field_name)
	;
	
	
	
	/**
	 * Returns item value
	 */
	lambda html getItemValue(string field_name, CrudController crud) =>
		crud.item.takeValue(field_name)
	;
	
	
	
	/**
	 * Extend field info
	 */
	lambda async Collection<string> extendsFieldsInfo(Dict<Collection> fields_info, CrudController crud) =>
		fields_info
	;
	
	
	
	/**
	 * Load field info
	 */
	static async list<RenderContainer, CrudController> loadFieldInfo(RenderContainer container, CrudController crud)
	{
		IntrospectionClass info = RuntimeUtils::getClassIntrospection(crud.model_name);
		
		/* Extends field info */
		Dict<Collection> fields_info = info.fields;
		fields_info = await static::extendsFieldsInfo(fields_info, crud);
		
		/* Get all components */
		Vector components = new Vector();
		fields_info.each
		(
			void (Collection<CoreStruct> annotations, string field_name) use (components)
			{
				FormValue form_value = annotations.findItem( lib::isInstance(classof FormValue) );
				if (form_value != null)
				{
					if (form_value.class_name != "") components.push(form_value.class_name);
				}
				TableValue table_value = annotations.findItem( lib::isInstance( classof TableValue ) );
				if (table_value != null)
				{
					if (table_value.class_name != "") components.push(table_value.class_name);
				}
			}
		);
		
		/* Extend layouts */
		crud <= fields_info <= info.fields;
		container <= layout <= modules <= [];
		container <= layout <= components <= components.toCollection();
		
		return [container, crud];
	}
	
	
	
	/**
	 * Extend layout
	 */
	static list<RenderContainer, CrudController> extendLayout(RenderContainer container, CrudController crud)
	{
		/* Extend modules */
		container <= layout <= modules <= container.layout.modules.concat( static::modules(crud) );
		
		/* Extend components */
		container <= layout <= components <= container.layout.components.concat( static::components(crud) );
		
		return [container, crud];
	}
	
	
	
	/**
	 * Returns table class name
	 */
	lambda html getTableClassName(string field_name, CrudController crud)
	{
		Collection<CoreStruct> annotations = crud.fields_info.get(field_name, null);
		if (annotations == null) return "";
		
		TableValue table_value = annotations.findItem( lib::isInstance( classof TableValue ) );
		if (table_value == null) return "";
		
		return table_value.class_name;
	}
	
	
	
	/**
	 * Returns forms class name
	 */
	lambda html getFormClassName(string field_name, CrudController crud)
	{
		Collection<CoreStruct> annotations = crud.fields_info.get(field_name, null);
		if (annotations == null) return "";
		
		FormValue form_value = annotations.findItem( lib::isInstance( classof FormValue ) );
		if (form_value == null) return "";
		
		return form_value.class_name;
	}
	
	
	
	/* -------------------- Search -------------------- */
	
	
	/* CrudController search */
	static async list<RenderContainer, CrudController> actionSearch(RenderContainer container, CrudController crud)
	{
		/* Set action */
		crud <= action <= "search";
		if (crud.api_name == "") crud <= api_name <= static::crudApiName(crud);
		if (crud.model_name == "") crud <= model_name <= static::crudModelName(crud);
		list res = [container, crud];
		
		/* Init Search */
		res = static::actionSearchInit(res[0], res[1]);
		
		/* Search do */
		res = await static::actionSearchApi(res[0], res[1]);
		
		/* Load Annotations */
		res = await static::loadFieldInfo(res[0], res[1]);
		
		/* Extend layout */
		res = static::extendLayout(res[0], res[1]);
		
		return res;
	}
	
	
	
	/**
	 * Search Init
	 */
	static list<RenderContainer, CrudController> actionSearchInit(RenderContainer container, CrudController crud)
	{
		Dict params = container.route_params;
		Dict query = container.request.query;
		Collection<string> fields = static::getTableFields(crud);
		/*Collection<CrudFilterItem> filter = (Collection<CrudFilterItem>) params.get("filter", null);*/
		
		int page = (int) query.get("page", 0);
		int limit = (int) query.get("limit", 30);
		if (limit <= 0) limit = 1;
		if (limit > crud.max_limit) limit = crud.max_limit;
		
		crud = crud.copy
		{
			"fields": fields,
			"page": page,
			"limit": limit,
			"start": page * limit,
			"item": rtl::newInstance(crud.model_name),
		};
		
		return [container, crud];
	}
	
	
	
	/**
	 * Search api request
	 */
	static async list<RenderContainer, CrudController> actionSearchApi
	(
		RenderContainer container, CrudController crud
	)
	{
		MessageRPC<Dict> api_res = await @->sendMessage
		(
			new MessageRPC
			{
				"api_name": crud.api_name,
				"space_name": "Runtime.Web.CRUD.CrudInterface",
				"method_name": "search",
				"data":
				{
					"fields": crud.fields,
					/*"filter": crud.filter,*/
					"order": crud.order,
					"page": crud.page,
					"limit": crud.limit,
				}
				"session": container.session,
			}
		);
		
		if (api_res->isSuccess())
		{
			OrmResult res = api_res.response;
			crud <= items <= res.items;
			crud <= total <= res.total;
		}
		else
		{
			crud <= error_code <= api_res.code;
			crud <= error_str <= api_res.error;
		}
		
		return [container, crud];
	}
	
	
	
	#ifdef BACKEND then
	
	/**
	 * Search Api
	 */
	static async MessageRPC apiSearch(MessageRPC msg, CrudController crud)
	{
		/* Set action */
		crud <= action <= "apiSearch";
		if (crud.model_name == "") crud <= model_name <= static::crudModelName(crud);
		
		DataProviderInterface provider = @->getProvider(classof DataProviderInterface, crud.orm_connection);
		
		string model_name = crud.model_name;
		string table_name = static::crudTableName();
		string fields = "";
		string where = "";
		string params = "";
		int page = 1;
		int limit = 30;
		
		OrmQuery q = new OrmQuery()
			-> select ( table_name, fields )
			-> asModel ( model_name, true )
			-> where ( where, params )
			-> offset ( page, limit )
		;
		
		OrmResult res = await q->execute(provider);
		return msg->success(res);
	}
	
	#endif
	
	
	
	/* ------------------ Add or Edit ----------------- */
	
	
	/* CrudController add */
	static async list<RenderContainer, CrudController> actionAdd(RenderContainer container, CrudController crud)
	{
		/* Set action */
		crud <= action <= "add";
		if (crud.api_name == "") crud <= api_name <= static::crudApiName(crud);
		if (crud.model_name == "") crud <= model_name <= static::crudModelName(crud);
		list res = [container, crud];
		
		/* Init Search */
		res = static::actionSaveInit(res[0], res[1]);
		
		/* Search do */
		res = await static::actionSaveDo(res[0], res[1]);
		
		/* Load Annotations */
		res = await static::loadFieldInfo(res[0], res[1]);
		
		/* Extend layout */
		res = static::extendLayout(res[0], res[1]);
		
		return res;
	}
	
	
	
	/* CrudController edit */
	static async list<RenderContainer, CrudController> actionEdit(RenderContainer container, CrudController crud)
	{
		/* Set action */
		crud <= action <= "edit";
		if (crud.api_name == "") crud <= api_name <= static::crudApiName(crud);
		if (crud.model_name == "") crud <= model_name <= static::crudModelName(crud);
		list res = [container, crud];
		
		/* Init Search */
		res = static::actionSaveInit(res[0], res[1]);
		
		/* Search do */
		res = await static::actionSaveApi(res[0], res[1]);
		
		/* Load Annotations */
		res = await static::loadFieldInfo(res[0], res[1]);
		
		/* Extend layout */
		res = static::extendLayout(res[0], res[1]);
		
		return res;
	}
	
	
	
	/**
	 * Save Init
	 */
	static list<RenderContainer, CrudController> actionSaveInit(RenderContainer container, CrudController crud)
	{
		int id = (int) container.route_params.get("id", 0);
		crud = crud.copy
		{
			"item": rtl::newInstance(crud.model_name),
			"pk":
			{
				"id": id,
			},
		};
		
		return [container, crud];
	}
	
	
	
	/**
	 * Save api request
	 */
	static async list<RenderContainer, CrudController> actionSaveApi(RenderContainer container, CrudController crud)
	{
		if (crud.action == "edit")
		{
			MessageRPC<Dict> api_res = await @->sendMessage
			(
				new MessageRPC
				{
					"api_name": crud.api_name,
					"space_name": "Runtime.Web.CRUD.CrudInterface",
					"method_name": "getByID",
					"data":
					{
						"pk": crud.pk,
					}
					"session": container.session,
				}
			);
			
			if (api_res->isSuccess())
			{
				ItemModel res = api_res.response;
				crud <= item <= res;
			}
			else
			{
				crud <= error_code <= api_res.code;
				crud <= error_str <= api_res.error;
			}
		}
		return [container, crud];
	}
	
	
	
	#ifdef BACKEND then
	
	
	/**
	 * Search Api
	 */
	static async MessageRPC apiGetByID(MessageRPC msg, CrudController crud)
	{
		/* Set action */
		crud <= action <= "apiGetByID";
		if (crud.model_name == "") crud <= model_name <= static::crudModelName(crud);
		
		/* Get connection */
		DataProviderInterface provider = @->getProvider(classof DataProviderInterface, crud.orm_connection);
		string model_name = crud.model_name;
		string table_name = static::crudTableName();
		
		/* Get data */
		Dict item = (Dict) rtl::attr(msg.data, ["pk"], null);
		
		/* Get data */
		Map pk_data = new Map();
		IntrospectionClass class_info = RuntimeUtils::getClassIntrospection(model_name);
		item.each
		(
			void (primitive value, string field_name) use (class_info, pk_data)
			{
				Collection<CoreStruct> annotations = class_info.fields.get(field_name, []);
				PrimaryKey primary = annotations.findItem( lib::isInstance(classof PrimaryKey) );
				FieldType field = annotations.findItem( lib::isInstance(classof FieldType) );
				if (primary != null)
				{
					if (field != null)
					{
						pk_data.set(field_name, value);
					}
				}
			}
		);
		
		OrmQuery q = new OrmQuery()
			-> select ( table_name, "" )
			-> asModel ( model_name, true )
			-> filter ( pk_data )
			-> offset (0, 1)
		;
		ItemModel res = await q->fetchOne(provider);
		return msg->success(res);
	}
	
	
	
	/**
	 * Add
	 */
	static async MessageRPC apiAdd(MessageRPC msg, CrudController crud)
	{
		/* Set action */
		crud <= action <= "apiAdd";
		if (crud.model_name == "") crud <= model_name <= static::crudModelName(crud);
		
		/* Get connection */
		DataProviderInterface provider = @->getProvider(classof DataProviderInterface, crud.orm_connection);
		string model_name = crud.model_name;
		string table_name = static::crudTableName();
		
		/* Get data */
		Dict item = (Dict) rtl::attr(msg.data, ["item"], null);
		
		/* Get data */
		Map item_data = new Map();
		IntrospectionClass class_info = RuntimeUtils::getClassIntrospection(model_name);
		item.each
		(
			void (primitive value, string field_name) use (class_info, item_data)
			{
				Collection<CoreStruct> annotations = class_info.fields.get(field_name, []);
				
				AutoIncrement auto = annotations.findItem( lib::isInstance(classof AutoIncrement) );
				PrimaryKey primary = annotations.findItem( lib::isInstance(classof PrimaryKey) );
				FieldType field = annotations.findItem( lib::isInstance(classof FieldType) );
				
				if (auto != null) return;
				if (field == null) return;
				item_data.set(field_name, value);
			}
		);
		
		/* Build query */
		OrmQuery q = new OrmQuery()
			-> insert ( table_name )
			-> asModel ( model_name, true )
			-> values( item_data )
		;
		
		OrmResult res = await q->execute(provider);
		return msg->success(res);
	}
	
	
	
	/**
	 * Save
	 */
	static async MessageRPC apiSave(MessageRPC msg, CrudController crud)
	{
		/* Set action */
		crud <= action <= "apiSave";
		if (crud.model_name == "") crud <= model_name <= static::crudModelName(crud);
		
		/* Get connection */
		DataProviderInterface provider = @->getProvider(classof DataProviderInterface, crud.orm_connection);
		string model_name = crud.model_name;
		string table_name = static::crudTableName();
		
		/* Get data */
		Dict item = (Dict) rtl::attr(msg.data, ["item"], null);
		
		/* Get data */
		Map item_data = new Map();
		Map pk_data = new Map();
		IntrospectionClass class_info = RuntimeUtils::getClassIntrospection(model_name);
		item.each
		(
			void (primitive value, string field_name) use (class_info, item_data, pk_data)
			{
				Collection<CoreStruct> annotations = class_info.fields.get(field_name, []);
				PrimaryKey primary = annotations.findItem( lib::isInstance(classof PrimaryKey) );
				FieldType field = annotations.findItem( lib::isInstance(classof FieldType) );
				if (primary != null)
				{
					if (field != null)
					{
						pk_data.set(field_name, value);
					}
					return;
				}
				if (field == null) return;
				item_data.set(field_name, value);
			}
		);
		
		/* Build query */
		OrmQuery q = new OrmQuery()
			-> update ( table_name )
			-> asModel ( model_name, true )
			-> values( item_data )
			-> filter ( pk_data )
		;
		
		OrmResult res = await q->execute(provider);
		return msg->success(res);
	}
	
	
	#endif
	
	
	/* -------------------- Delete -------------------- */
	
	
	/* CrudController delete */
	static async list<RenderContainer, CrudController> actionDelete(RenderContainer container, CrudController crud)
	{
		/* Set action */
		crud <= action <= "delete";
		if (crud.api_name == "") crud <= api_name <= static::crudApiName(crud);
		if (crud.model_name == "") crud <= model_name <= static::crudModelName(crud);
		list res = [container, crud];
		
		/* Init Delete */
		res = static::actionDeleteInit(res[0], res[1]);
		
		/* Delete do */
		res = await static::actionDeleteApi(res[0], res[1]);
		
		/* Load Annotations */
		res = await static::loadFieldInfo(res[0], res[1]);
		
		/* Extend layout */
		res = static::extendLayout(res[0], res[1]);
		
		return res;
	}
	
	
	
	/**
	 * Delete Init
	 */
	static list<RenderContainer, CrudController> actionDeleteInit
	(
		RenderContainer container, CrudController crud
	)
	{
		int id = (int) container.route_params.get("id", 0);
		crud = crud.copy
		{
			"pk":
			{
				"id": id,
			},
		};
		
		return [container, crud];
	}
	
	
	
	/**
	 * Delete api request
	 */
	static async list<RenderContainer, CrudController> actionDeleteApi
	(
		RenderContainer container, CrudController crud
	)
	{
		MessageRPC<Dict> api_res = await @->sendMessage
		(
			new MessageRPC
			{
				"api_name": crud.api_name,
				"space_name": "Runtime.Web.CRUD.CrudInterface",
				"method_name": "getByID",
				"data":
				{
					"pk": crud.pk,
				}
				"session": container.session,
			}
		);
		
		if (api_res->isSuccess())
		{
			ItemModel res = api_res.response;
			crud <= item <= res;
		}
		else
		{
			crud <= error_code <= api_res.code;
			crud <= error_str <= api_res.error;
		}
		
		return [container, crud];
	}
	
	
	
	#ifdef BACKEND then
	
	/**
	 * Delete Api
	 */
	static async MessageRPC apiDelete(MessageRPC msg, CrudController crud)
	{
		/* Set action */
		crud <= action <= "apiDelete";
		if (crud.model_name == "") crud <= model_name <= static::crudModelName(crud);
		
		/* Get connection */
		DataProviderInterface provider = @->getProvider(classof DataProviderInterface, crud.orm_connection);
		string model_name = crud.model_name;
		string table_name = static::crudTableName();
		
		/* Get data */
		Dict item = (Dict) rtl::attr(msg.data, ["item"], null);
		
		/* Get data */
		Map pk_data = new Map();
		IntrospectionClass class_info = RuntimeUtils::getClassIntrospection(model_name);
		item.each
		(
			void (primitive value, string field_name) use (class_info, pk_data)
			{
				Collection<CoreStruct> annotations = class_info.fields.get(field_name, []);
				PrimaryKey primary = annotations.findItem( lib::isInstance(classof PrimaryKey) );
				FieldType field = annotations.findItem( lib::isInstance(classof FieldType) );
				if (primary != null)
				{
					if (field != null)
					{
						pk_data.set(field_name, value);
					}
					return;
				}
			}
		);
		
		/* Build query */
		OrmQuery q = new OrmQuery()
			-> delete ( table_name )
			-> filter ( pk_data )
		;
		
		OrmResult res = await q->execute(provider);
		return msg->success(res);
	}
	
	#endif
	
}
